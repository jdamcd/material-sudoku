apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId 'com.jdamcd.sudoku'
        minSdkVersion 23
        targetSdkVersion 30
        flavorDimensions 'default'
        versionCode Integer.parseInt(project.SUDOKU_VERSION_CODE)
        versionName project.SUDOKU_VERSION_NAME + '-' + versionCode
        project.ext.set('archivesBaseName', 'sudoku-' + versionName)

        testApplicationId "com.jdamcd.sudoku.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    productFlavors {
        google {
            buildConfigField 'boolean', 'GOOGLE', 'true'
        }

        vanilla {
            buildConfigField 'boolean', 'GOOGLE', 'false'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFile 'proguard-project.txt'
        }
    }

    lintOptions {
        disable 'IncludeLayoutParam'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    ext.dagger_version = '2.26'
    ext.anko_version = '0.10.8'
    ext.room_version = '2.2.5'

    implementation project(':core')

    // Support
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Architecture
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation('com.nhaarman:mockito-kotlin:1.6.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation 'androidx.annotation:annotation:1.1.0'

    // Android tests
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"

    androidTestUtil 'androidx.test:orchestrator:1.3.0'
}
